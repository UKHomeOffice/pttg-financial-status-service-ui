buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://github.com/UKHomeOffice/pttg-gradle-repo/raw/master/releases" }

        // move to ui plugin
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {

        classpath 'pttg-gradle-common:pttgCommonGradle:1.1.RELEASE'
        classpath 'pttg-gradle-common:pttgSpringBootGradle:1.0.RELEASE'

        classpath 'net.serenity-bdd:serenity-gradle-plugin:1.1.31'

        // move to ui plugin
        classpath("com.moowork.gradle:gradle-node-plugin:0.12")
        classpath("com.moowork.gradle:gradle-gulp-plugin:0.12")
    }
}

apply plugin: 'pttgCommonGradle'
apply plugin: 'pttgSpringBootGradle'
apply plugin: 'net.serenity-bdd.aggregator'

// move to ui plugin
apply plugin: 'com.moowork.gulp'

group 'pttg-financial-status-service-ui'
version '1.0-SNAPSHOT'

mainClassName = "uk.gov.digital.ho.proving.financial.ServiceRunner"  // used by application plugin

dependencies {
    compile libraries.logging
    compile libraries.springboot

    testCompile libraries.cucumber
}

springboot{
    port = 8001
}

// move to ui plugin
jar.into('static') {
    from('src/main/webapp')
}

// move to ui plugin
build.dependsOn gulp_build

// to do - consider making acceptanceTest config part of common-gradle plugin
test {
    exclude 'acceptance/**'
}

task acceptanceTest(type: Test) {
    description 'Runs the acceptance tests.'
    group 'verification'
    include 'acceptance/**'
    finalizedBy aggregate
    outputs.upToDateWhen {false}
}

