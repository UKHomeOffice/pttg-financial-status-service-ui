buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://github.com/UKHomeOffice/pttg-gradle-repo/raw/master/releases" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {

        classpath 'pttg-gradle-common:pttgCommonGradle:1.2.RELEASE'
        classpath 'pttg-gradle-common:pttgSpringBootGradle:1.1.RELEASE'

        classpath 'net.serenity-bdd:serenity-gradle-plugin:1.1.31'

        classpath "com.moowork.gradle:gradle-node-plugin:0.12"
        classpath "com.moowork.gradle:gradle-gulp-plugin:0.12"
    }
}

apply plugin: 'pttgCommonGradle'
apply plugin: 'pttgSpringBootGradle'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'com.moowork.gulp'

group 'pttg-financial-status-service-ui'
version '1.0-SNAPSHOT'

mainClassName = "uk.gov.digital.ho.proving.financial.ServiceRunner"  // used by application plugin

dependencies {
    compile libraries.groovy
    compile libraries.jackson
    compile libraries.jersey
    compile libraries.json
    compile libraries.logging

    compile 'com.github.tomakehurst:wiremock:1.58'

    testCompile libraries.cucumber
    testCompile libraries.groovy
    testCompile libraries.jackson
    testCompile libraries.jersey
    testCompile libraries.json
    testCompile libraries.spock
    testCompile libraries.springTest
    testCompile libraries.testUtils
}

springboot{
    port = 8001
}

jar.into('static') {
    from('src/main/webapp')
}

test {
    exclude 'acceptance/**'
}

task acceptanceTest(type: Test) {
    description 'Runs the acceptance tests.'
    group 'verification'
    include 'acceptance/**'
    finalizedBy aggregate
    outputs.upToDateWhen {false}
}

processResources {
    filesMatching("**/application.properties") {
        expand( project.properties )
    }
}

clean.doFirst{
    delete 'pttg-financial-status-service-ui.log'
}
