buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://github.com/UKHomeOffice/pttg-gradle-repo/raw/master/releases" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {

        classpath 'pttg-gradle-common:pttgCommonGradle:1.1.RELEASE'
        classpath 'pttg-gradle-common:pttgSpringBootGradle:1.0.RELEASE'

        classpath 'net.serenity-bdd:serenity-gradle-plugin:1.1.31'

        classpath("com.moowork.gradle:gradle-node-plugin:0.12")
        classpath("com.moowork.gradle:gradle-gulp-plugin:0.12")

        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.16"
    }
}

apply plugin: 'pttgCommonGradle'
apply plugin: 'pttgSpringBootGradle'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'com.moowork.gulp'
apply plugin: 'com.gorylenko.gradle-git-properties'

group 'pttg-financial-status-service-ui'
version '1.0-SNAPSHOT'

mainClassName = "uk.gov.digital.ho.proving.financial.ServiceRunner"  // used by application plugin

dependencies {
    compile libraries.logging
    compile libraries.springbootDev

    compile 'com.sun.jersey:jersey-client:1.19'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.4'


    testCompile libraries.cucumber
    testCompile 'org.assertj:assertj-core:3.4.1'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

springboot{
    port = 8001
}

jar.into('static') {
    from('src/main/webapp')
}

build.dependsOn gulp_build, gulp_test

task("gulpTest", type: Test){
    dependsOn gulp_test
    group 'verification'
}

test {
    exclude 'acceptance/**'
}

task acceptanceTest(type: Test) {
    description 'Runs the acceptance tests.'
    group 'verification'
    include 'acceptance/**'
    finalizedBy aggregate
    outputs.upToDateWhen {false}
}

bootRun {
    systemProperties = [
        'endpoints.health.sensitive': false
    ]
}

processResources {
    filesMatching("**/application.properties") {
        expand( project.properties )
    }
}
